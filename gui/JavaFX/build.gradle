plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'org.beryx.jlink' version '3.1.1' 
}

import org.gradle.internal.os.OperatingSystem
def os = OperatingSystem.current()

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.google.code.gson:gson:2.13.1'
}

javafx {
    version = '21'
    modules = [ 'javafx.controls', 'javafx.fxml' ]
}

application {
    mainClass = 'com.parallelbenchmark.Main'
    mainModule = 'parallelbenchmark'
}

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'parallelbenchmark'
    }
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

task copyBenchmarks(type: Copy) {
    into "$buildDir"

    if (os.isWindows()) {
        from('../../build/debug/parallel_benchmark.exe') {
            into 'benchmarks/bin'
        }
    } else if (os.isLinux()) {
        from('../../build/bin/parallel_benchmark') {
            into 'benchmarks/bin'
        }
    }

    from('../../build/kernels') {
        into 'benchmarks/kernels'
    }
}

jar.dependsOn(copyBenchmarks)